// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM4K.hdl

/**
 * Memory of 4K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, sel=address[9..11], a=ram0Load, b=ram1Load, c=ram2Load, d=ram3Load, e=ram4Load, f=ram5Load, g=ram6Load, h=ram7Load);
    RAM512(in=in, load=ram0Load, address=address[0..8], out=ram0Output);
    RAM512(in=in, load=ram1Load, address=address[0..8], out=ram1Output);
    RAM512(in=in, load=ram2Load, address=address[0..8], out=ram2Output);
    RAM512(in=in, load=ram3Load, address=address[0..8], out=ram3Output);
    RAM512(in=in, load=ram4Load, address=address[0..8], out=ram4Output);
    RAM512(in=in, load=ram5Load, address=address[0..8], out=ram5Output);
    RAM512(in=in, load=ram6Load, address=address[0..8], out=ram6Output);
    RAM512(in=in, load=ram7Load, address=address[0..8], out=ram7Output);
    Mux8Way16(a=ram0Output, b=ram1Output, c=ram2Output, d=ram3Output, e=ram4Output, f=ram5Output, g=ram6Output, h=ram7Output, sel=address[9..11], out=out);
}
